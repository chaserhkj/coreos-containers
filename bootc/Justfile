mod arch-config 'arch-host-config/'

build_flags := env("PODMAN_BUILD_FLAGS", "")
arch_bootc_base := env("ARCH_BOOTC_BASE", "")

default:
    just build-image arch-k0s
    just build-image arch-common-config
    just build-image arch-common-home-config
    just build-variant-image arch-common-config arch-k0s
    just build-variant-image arch-common-home-config arch-k0s

build-all:
    just build-base-images
    just default

build-base-images:
    just build-image arch-base {{arch_bootc_base}}
    just build-live-images arch {{arch_bootc_base}}

build-live-images distro bootc_base="":
    from_flag="" ; [ -n "{{bootc_base}}" ] && from_flag="--build-arg BOOTC_BASE={{bootc_base}}"; \
    podman build {{distro}}-live --target builder -t bootc-{{distro}}-live-builder {{build_flags}} $from_flag && \
    podman build {{distro}}-live -t bootc-{{distro}}-live {{build_flags}} $from_flag

build-image subdir bootc_base="":
    from_flag="" ; [ -n "{{bootc_base}}" ] && from_flag="--build-arg BOOTC_BASE={{bootc_base}}"; \
    podman build {{subdir}} -t bootc-{{subdir}} {{build_flags}} $from_flag

build-variant-image subdir base:
    #!/usr/bin/env bash
    set -e
    postfix=$(echo "{{base}}" | sed "s/.*-//g")
    manifest={{subdir}}/Containerfile
    # Resolve base image
    base_arg=$(grep "ARG BASE=.*$" $manifest| head -n1)
    if [[ -n "$base_arg" ]]; then
        orig_base_image=$(echo $base_arg | sed "s/^ARG BASE=//")
    else
        orig_base_image=$(grep "^FROM" $manifest|tail -n1|choose 1)
    fi
    [[ $orig_base_image == *-base ]] && base_image=bootc-{{base}} || base_image=$orig_base_image-$postfix
    echo "Use base $base_image for bootc-{{subdir}}-$postfix"
    podman build {{subdir}} -t bootc-{{subdir}}-$postfix --build-arg BASE=$base_image {{build_flags}}