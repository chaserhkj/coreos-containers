set dotenv-load := true
podman := require("podman")
image_ref := env("ARCH_BOOTC_HOST_IMAGE_REF", "docker.io/chaserhkj/arch-bootc-host")

default: prepare-all-images

prepare-all-images:
    just prepare-image hkj-laptop
    just prepare-image kagumine

prepare-image host: (configure-image host)
    podman tag localhost/bootc-arch-host:{{host}} {{image_ref}}:{{host}}
    podman push {{image_ref}}:{{host}}

configure-image host:
    #!/usr/bin/env bash
    host_dir="hosts/{{host}}"
    [ -f $host_dir/BASE ] && read -r base_img < "$host_dir/BASE"
    [ -n $base_img ] && base_flag="--from=$base_img"
    export CREDS="$(gpg --decrypt creds.gpg)" || exit 1
    if [ -f $host_dir/Containerfile ]; then
        podman build --secret=id=creds,env=CREDS --iidfile {{host}}.intermediate.id -f Containerfile "$base_flag" "$host_dir"
        read -r intermediate_img < {{host}}.intermediate.id
        podman build -t localhost/bootc-arch-host:{{host}} --build-arg BASE=$intermediate_img "$host_dir"
    else
        podman build --secret=id=creds,env=CREDS -t localhost/bootc-arch-host:{{host}} -f Containerfile "$base_flag" "$host_dir"
    fi
